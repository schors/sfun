;=================================================================
;|               double TAN(double X)      27.02.98   Ф.К.       |
;=================================================================

	.TITLE TAN
	.RADIX 8

	.IDENT /V1.0/

	.PSECT .SFUN.

TAN::	SETD
	ABSF	%0

;**************************************************
; приведение аргумента к полукругу

CIR.2:	LDF	%0,%1		
	CMPF    M.PI,%0
	CFCC
	SUBF	M.PI,%0
	BLE	CIR.2

;**************************************************
; приведение аргумента к четверти круга

	CMPF	M.PI.2,%1       
	CFCC	
	BGT	CMP2
	BEQ	E1		; переход если X=Pi/2
	SUBF	M.PI,%1
	ABSF	%1	
	COM	2(SP)

;**************************************************
; сравнение с 0

CMP2:	TSTF	%1		
	CFCC	
	BEQ	E0		; переход если X=0

;**************************************************
; сравнение с Pi/4

	MOV	#1,4(SP) 	; установка признака угла X <= Pi/4
	CMPF	M.PI.4,%1	
	CFCC
	BEQ	E2		; переход, если X=Pi/4
	BGT	TAN0		; переход, если X <= Pi/4 	

;**************************************************
; приведение аргумента к Pi/4

	CLR	4(SP)		; установка признака угла X > Pi/4
	SUBF	M.PI.2,%1	; приведение X к Pi/4
	ABSF	%1

TAN0:	LDF	%1,%0		; возведение X в квадрат
	MULF	%1,%0

;**************************************************
;| Вычисление значения функции			  |
;**************************************************
	
	LDF	%0,%2 		; расчет первого полинома
	NEGF	%2    		
	ADDF	TANC5,%2
	MULF	%0,%2
	ADDF	TANC4,%2
	MULF	%0,%2
	ADDF	TANC1,%2
	
	LDF	TANC3,%3	; расчет второго полинома
	MULF	%0,%3
	ADDF	TANC2,%3
	MULF	%0,%3
	ADDF	TANC1,%3
	MULF	%1,%3
	
	TST	4(SP)		; проверка признака угла X < PI/4
	BEQ	TAN1
	DIVF	%2,%3
	LDF	%3,%0
	BR	Q1
TAN1:	DIVF	%3,%2
	LDF	%2,%0

Q1:	TST	2(SP)		; корректировка знака
	BGE	Q2
	NEGF	%0

;**************************************************
;| Выход					  |
;**************************************************

Q2:	RTS	PC	

;**************************************************
;| Возвраты фиксированных значений функции	  |
;**************************************************

E0:	CLRF	%0		; 0, при X = 0
	RTS	PC
E2:	LDF	M.1,%0		; 1, при X = PI/4
	BR	Q1

;**********************************************************************
;|Выход при недопустимом аргументе X = PI/2 /+-/ PI*n, где n=1,2,3... |
;**********************************************************************

E1:	LDF	HUGE,%0		
	RTS	PC

	.END

