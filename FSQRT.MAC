;=========================================================
;|        double SQRT(double X)      27.02.98    Ф.К.    |
;=========================================================

		.TITLE SQRT
		.RADIX 8

		.IDENT /V1.0/

		.PSECT .SFUN.

SQRT::		SETD
		TST	2(SP)
		BLT	E1		; выход, если X < 0
		TSTF	%0		
		CFCC
		BEQ	E0		; переход, если X = 0
		CMPF	M.1,%0
		CFCC
		BEQ	E0		; переход, если X = 1

;*****************************************
; Приведение аргумента к пределам (0,1)

		STEXP	%0,2(SP)	; сохранение экспоненты
		LDEXP	#0,%0		

;*****************************************
; Выбор начального приближения 

		CMPF	SINC2,%0
		CFCC
		BGE	L2		; переход, если X <= 0.25
		CMPF	SINC1,%0
		CFCC
		BGE	L1		; переход, если 0.25 <= X <= 0.5

;******************************************
; Расчеты начальных приближений

		LDF	SQRT12,%1	; 0.5 < X < 1  
		LDF	SQRT11,%2
		ADDF	%0,%1
		DIVF	%1,%2
		LDF	SQRT10,%1
		SUBF	%2,%1
		BR	S0

L1:		LDF	SQRT02,%1	; 0.25 < X <= 0.5
		LDF	SQRT01,%2
		ADDF	%0,%1
		DIVF	%1,%2
		LDF	SQRT00,%1
		SUBF	%2,%1
		BR	S0

L2:		LDF	%0,%1		; 0.0 < X <= 0.25

;****************************************************************
;| Расчет квадратного корня  ( три итерации по формуле Герона ) |
;****************************************************************

S0:		LDF	%0,%2		; условно 1-ая
		DIVF	%1,%2
		ADDF	%1,%2		;   AC2 <- 2*Y1

		LDF	%0,%1		; условно 2-ая
		LDF	SINC2,%3
		MULF	%2,%3
		DIVF	%2,%1
		ADDF	%3,%1		;   AC1 <- Y2
		
		LDF	SINC1,%3	; 3-я
		DIVF	%1,%0
		ADDF	%1,%0
		MULF	%3,%0

;************************************************
; Расчет и установка нового значения экспоненты 

L3:		ASR	2(SP)
		BCC	S1
		MULF	SQRT.2,%0
		CLC
S1:		STEXP	%0,4(SP)
		ADD	4(SP),2(SP)
		LDEXP	2(SP),%0

;************************************************
; Выход

E0:		RTS	PC		

;************************************************
; Выход при отрицательном аргументе

E1:		LDF	M.1,%0
		NEGF	%0
		RTS	PC
		
		.END

