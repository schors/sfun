;*****************************************************************
;|	double ATAN2(double X,double Y)                          |
;*****************************************************************

	.TITLE	ATAN
	.RADIX	8
	.PSECT	.SFUN.
	.IDENT/V3.0/
	;/26.03.98г./

;***************************************************************
;|          Вход арктангенса                                   |
;***************************************************************

ATAN::		SETD
		ABSF	%0
		TSTF	%0		;  Сравнение с 0 
		CFCC	
		BEQ	QQ

		CMPF	M.1,%0		;  Сравнение с 1
		CFCC
		BEQ	Q1

		CLR	6(SP)		; обнуление признака обр. арг.

		MOV	#ATNS2,4(SP)	; начальное значение "счетчика"
					; интервалов

		CLRF	%3

;********************************************
;     определение принадлености интервалам

		CMPF	ATNS1,%0	
		CFCC
		BGT	ATANP		; переход к расчету полинома
		CMPF	ATNS3,%0    
		CFCC
		BGT	ATANT		; переход к пересчету аргумента 

		ADD	#20,4(SP)	; увеличение "счетчика" /ATANS4
		CMPF	ATNS5,%0
		CFCC
		BGT	ATANT		; переход к пересчету аргумента 

		ADD	#20,4(SP)	; увеличение "счетчика" /ATANS6
		CMPF	ATNS7,%0
		CFCC
		BGT	ATANT		; переход к пересчету аргумента 

		ADD	#20,4(SP)	; увеличение "счетчика" /ATANS8
		CMPF	ATNS9,%0
		CFCC
		BGT	ATANT		; переход к пересчету аргумента 

		ADD	#20,4(SP)	; увеличение "счетчика" /ATANS10
		CMPF	ATNS11,%0
		CFCC
		BGT	ATANT		; переход к пересчету аргумента 

		COM	6(SP)		; признак обратного аргумента
		LDF	M.PI.2,%3
                LDF	M.1,%1		; обратный аргумент
		DIVF	%0,%1
		LDF	%1,%0
		BR	ATANP		; переход к расчету полинома

ATANT:		LDF	@4(SP),%3	; пересчет аргумента
		LDF	%0,%1
		MULF	%3,%1
		SUBF	%3,%0
		ADDF	M.1,%1
		DIVF	%1,%0
		ADD	#10,4(SP)
		LDF	@4(SP),%3

;*************************************************************
;       Расчет полинома

ATANP:		LDF	%0,%1		; расчет полинома
		MULF	%0,%1
		LDF	ATNC9,%2
		MULF	%1,%2
		ADDF	ATNC7,%2
		MULF	%1,%2
		ADDF	ATNC5,%2
		MULF	%1,%2
		ADDF	ATNC3,%2
		MULF	%1,%2
		ADDF	ATNC1,%2
		MULF	%2,%0

;*****************************************************************
;           Корректировка результатов вычислений

		TST	6(SP)		; проверка на обратный аргумент
		BEQ	ATANN
		SUBF	%0,%3		; вычисление при обратном аргументе
		LDF	%3,%0
		BR	Q

ATANN:		ADDF	%3,%0		; вычисление при нормальном аргументе

Q:		TST	2(SP)		; корректировка знака
		BGT	QQ
		NEGF	%0

;**********************************************************
;         Выход

QQ:		RTS	PC

;**********************************************************
;    Выход  с фиксированными значениями функции

Q1:		LDF	M.PI.4,%0
		BR	Q

		.END
