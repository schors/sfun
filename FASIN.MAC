;==============================================================
;|                double ASIN(double X)    27.02.98   Ф.К.    |
;==============================================================

	.TITLE	ASIN
	.RADIX	8

	.IDENT /V1.0/

	.PSECT	.SFUN.

ASIN::		SETD
		ABSF	%0
		MOV	#0,6(SP)
		TSTF	%0
		CFCC	
		BEQ	A2		; переход, если X = 0.0
		CMPF	M.1,%0
		CFCC
		BEQ	A3		; переход, если X = 1.0
		BLT	A4		; переход, если X > 1.0
		LDF	%0,%1
		MULF	%0,%1

;******************************************************************
;|	Приведение аргумента к пределам   [SIN(0),SIN(PI/4)]      |
;******************************************************************

		CMPF	SINC1,%1
		CFCC
		BGT	A0		; переход, если X~2 < 0.5
		BEQ	A1		; переход, если X~2 = 0.5

;*********************************************
; Пересет аргумента при значении X > SIN(PI/4)

		LDF	M.1,%0		; 1-X~2
		SUBF	%1,%0
		STF	%0,%4

		STEXP	%0,4(SP)	; SQRT(1-X~2)
		LDEXP	#0,%0
		CMPF	SINC2,%0
		CFCC
		BGE	L2		; переход, если X <= 0.25

		LDF	SQRT02,%1
		LDF	SQRT01,%2
		ADDF	%0,%1
		DIVF	%1,%2
		LDF	SQRT00,%1
		SUBF	%2,%1
		BR	S0

L2:		LDF	%0,%1

S0:		LDF	%0,%2
		DIVF	%1,%2
		ADDF	%1,%2		;   AC2 <- 2*Y1

		LDF	%0,%1
		LDF	SINC2,%3
		MULF	%2,%3
		DIVF	%2,%1
		ADDF	%3,%1

		LDF	SINC1,%3
		DIVF	%1,%0
		ADDF	%1,%0
		MULF	%3,%0

		CCC
		ASR	4(SP)
		BCC	S1
		MULF	SQRT.2,%0
S1:		CLC
		STEXP	%0,6(SP)
		ADD	6(SP),4(SP)
		LDEXP	4(SP),%0
		LDF	%4,%1
		MOV	#1,6(SP)	; установка признака пересчета

;**********************************************************************
;|	Вычисление ASIN(X), в AC0 <- X, в AC1 <- X~2		      |
;**********************************************************************

A0:		LDF	ASIN21,%2	
		MULF	%1,%2
		ADDF	ASIN19,%2
		MULF	%1,%2
		ADDF	ASIN17,%2
		MULF	%1,%2
		ADDF	ASIN15,%2
		MULF	%1,%2
		ADDF	ASIN13,%2
		MULF	%1,%2
		ADDF	ASIN11,%2
		MULF	%1,%2
		ADDF	ASIN9,%2
		MULF	%1,%2
		ADDF	ASIN7,%2
		MULF	%1,%2
		ADDF	ASIN5,%2
		MULF	%1,%2
		ADDF	ASIN3,%2
		MULF	%1,%2
		ADDF	ASIN1,%2
		MULF	%2,%0

;***************************************
; Проверка на наличие пересчета

		TST	6(SP)
		BEQ	E0		; переход, если не было пересчета
		SUBF	M.PI.2,%0
		NEGF	%0

;***************************************
; Установка знака и выход

E0:		TST	2(SP)
		BLT	E1		; переход, если меньше 0
		RTS	PC
E1:		NEGF	%0
A2:		RTS	PC

;******************************************
; Выход с фиксированным значением функции

A1:		LDF	M.PI.4,%0
		BR	E0
A3:		LDF	M.PI.2,%0
		BR	E0

;******************************************
; Выход при недопустимом аргументе

A4:		LDF	HUGE,%0
		RTS	PC

		.END

