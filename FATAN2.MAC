;****************************************************************
;|     double ATAN2(double Y,ouble X)  19.03.98      Ф.К.       |
;****************************************************************

	.TITLE	ATAN2
	.RADIX	8
	.PSECT	.SFUN.
	.IDENT/V3.1/
	;/19.04.98г./

;**************************************************************
;|      Вход ATAN2                                            |
;**************************************************************

ATAN2::		SETD

		CLRF	%3
		CLRF	%4

		LDF	12(SP),%1	; загрузка X в AC1

		TSTF	%1
		CFCC
		BGT	L1		; переход, если X > 0
		BEQ	L2		; переход, если X = 0

		LDF	M.PI,%2		; добавка
                STF	%2,%4 

L1:		TSTF	%0
		CFCC
		BEQ	L3		; переход, если Y = 0

		LDF	%0,%2
		DIVF	%1,%2
		STF	%2,2(SP)
		ABSF	%2

		CMPF	M.1,%2
		CFCC
		BEQ	L4		; переход, если X = Y
		BR ATANE

;**********************************************************
;    Выход  с фиксированными значениями функции


L2:		TSTF	%0
		CFCC
		BEQ	L22
		LDF	M.PI.2,%0
		BGT	L22
		NEGF	%0

L22:		RTS	PC

L3:		LDF	%4,%0
		RTS	PC

L4:		LDF	M.PI.4,%0
		BR	Q
		
;********************************************
;     определение принадлености интервалам

ATANP0:		LDF	%2,%0
		BR	ATANP

ATANE:		MOV	#ATNS2,4(SP)	; начальное значение "счетчика"
		CLR	6(SP)		; обнуление признака обр. арг.

		CMPF	ATNS11,%2	; переход для расчета обратного
		CFCC			; аргумента
		BLT	ATANT1		; переход к пересчету аргумента 

		CMPF	ATNS1,%2	
		CFCC
		BGT	ATANP0		; переход к расчету полинома

		CMPF	ATNS3,%2    
		CFCC
		BGT	ATANT		; переход к пересчету аргумента 

		ADD	#20,4(SP)	; увеличение "счетчика" /ATANS4
		CMPF	ATNS5,%2
		CFCC
		BGT	ATANT		; переход к пересчету аргумента 

		ADD	#20,4(SP)	; увеличение "счетчика" /ATANS6
		CMPF	ATNS7,%2
		CFCC
		BGT	ATANT		; переход к пересчету аргумента 

		ADD	#20,4(SP)	; увеличение "счетчика" /ATANS8
		CMPF	ATNS9,%2
		CFCC
		BGT	ATANT		; переход к пересчету аргумента 

		ADD	#20,4(SP)	; увеличение "счетчика" /ATANS10
		BR	ATANT		; переход к пересчету аргумента 

ATANT1:		COM	6(SP)		; признак обратного аргумента
		LDF	M.PI.2,%3
		DIVF	%0,%1		; обратный аргумент
		ABSF	%1
		LDF	%1,%0
		BR	ATANP1		; переход к расчету полинома

ATANT:		LDF	%2,%0		; пересчет аргумента
		LDF	@4(SP),%1	; при входе AC2 = Y/X
		MULF	%1,%2
		SUBF	%1,%0
		ADDF	M.1,%2
		DIVF	%2,%0
		ADD	#10,4(SP)
		LDF	@4(SP),%3

;*************************************************************
;       Расчет полинома

ATANP:		LDF	%0,%1		; расчет полинома
ATANP1:		MULF	%0,%1
		LDF	ATNC9,%2
		MULF	%1,%2
		ADDF	ATNC7,%2
		MULF	%1,%2
		ADDF	ATNC5,%2
		MULF	%1,%2
		ADDF	ATNC3,%2
		MULF	%1,%2
		ADDF	ATNC1,%2
		MULF	%2,%0

;*****************************************************************
;           Корректировка результатов вычислений

		TST	6(SP)		; проверка на обратный аргумент
		BEQ	ATANN
		SUBF	%0,%3		; вычисление при обратном аргументе
		LDF	%3,%0
		BR	Q

ATANN:		ADDF	%3,%0		; вычисление при нормальном аргументе

Q:		TST	2(SP)		; корректировка знака
		BGT	QQ
		NEGF	%0
		NEGF	%4

;**********************************************************
;         Выход

QQ:		SUBF	%4,%0		; поправка из ATAN2
		RTS	PC

		.END
